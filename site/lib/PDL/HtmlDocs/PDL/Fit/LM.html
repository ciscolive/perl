<?xml version="1.0" ?>
<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Strict//EN" "http://www.w3.org/TR/xhtml1/DTD/xhtml1-strict.dtd">
<html xmlns="http://www.w3.org/1999/xhtml">
<head>
<title>PDL::Fit::LM -- Levenberg-Marquardt fitting routine for PDL</title>
<meta http-equiv="content-type" content="text/html; charset=utf-8" />
<link rev="made" href="mailto:WENWU@mojo.(none)" />
</head>

<body>



<ul id="index">
  <li><a href="#NAME">NAME</a></li>
  <li><a href="#DESCRIPTION">DESCRIPTION</a></li>
  <li><a href="#SYNOPSIS">SYNOPSIS</a></li>
  <li><a href="#FUNCTIONS">FUNCTIONS</a>
    <ul>
      <li><a href="#lmfit">lmfit</a></li>
      <li><a href="#tlmfit">tlmfit</a></li>
    </ul>
  </li>
  <li><a href="#BUGS">BUGS</a></li>
  <li><a href="#AUTHOR">AUTHOR</a></li>
</ul>

<h1 id="NAME">NAME</h1>

<p>PDL::Fit::LM -- Levenberg-Marquardt fitting routine for PDL</p>

<h1 id="DESCRIPTION">DESCRIPTION</h1>

<p>This module provides fitting functions for PDL. Currently, only Levenberg-Marquardt fitting is implemented. Other procedures should be added as required. For a fairly concise overview on fitting see Numerical Recipes, chapter 15 &quot;Modeling of data&quot;.</p>

<h1 id="SYNOPSIS">SYNOPSIS</h1>

<pre><code> use PDL::Fit::LM;
 $ym = lmfit $x, $y, $sigma, \&amp;expfunc, $initp, {Maxiter =&gt; 300};</code></pre>

<h1 id="FUNCTIONS">FUNCTIONS</h1>

<h2 id="lmfit">lmfit</h2>

<p>Levenberg-Marquardt fitting of a user supplied model function</p>

<pre><code> ($ym,$finalp,$covar,$iters) =
      lmfit $x, $y, $sigma, \&amp;expfunc, $initp, {Maxiter =&gt; 300, Eps =&gt; 1e-3};</code></pre>

<p>where $x is the independent variable and $y the value of the dependent variable at each $x, $sigma is the estimate of the uncertainty (i.e., standard deviation) of $y at each data point, the fourth argument is a subroutine reference (see below), and $initp the initial values of the parameters to be adjusted.</p>

<p>Options:</p>

<pre><code> Maxiter:  maximum number of iterations before giving up
 Eps:      convergence criterion for fit; success when normalized change
           in chisquare smaller than Eps</code></pre>

<p>The user supplied sub routine reference should accept 4 arguments</p>

<ul>

<li><p>a vector of independent values $x</p>

</li>
<li><p>a vector of fitting parameters</p>

</li>
<li><p>a vector of dependent variables that will be assigned upon return</p>

</li>
<li><p>a matrix of partial derivatives with respect to the fitting parameters that will be assigned upon return</p>

</li>
</ul>

<p>As an example take this definition of a single exponential with 3 parameters (width, amplitude, offset):</p>

<pre><code> sub expdec {
   my ($x,$par,$ym,$dyda) = @_;
   my ($width,$amp,$off) = map {$par-&gt;slice(&quot;($_)&quot;)} (0..2);
   my $arg = $x/$width;
   my $ex = exp($arg);
   $ym .= $amp*$ex+$off;
   my (@dy) = map {$dyda-&gt;slice(&quot;,($_)&quot;)} (0..2);
   $dy[0] .= -$amp*$ex*$arg/$width;
   $dy[1] .= $ex;
   $dy[2] .= 1;
 }</code></pre>

<p>Note usage of the <code>.=</code> operator for assignment</p>

<p>In scalar context returns a vector of the fitted dependent variable. In list context returns fitted y-values, vector of fitted parameters, an estimate of the covariance matrix (as an indicator of goodness of fit) and number of iterations performed.</p>

<p>An extended example script that uses lmfit is included below. This nice example was provided by John Gehman and should help you to master the initial hurdles. It can also be found in the <i>Example/Fit</i> directory.</p>

<pre><code>   use PDL;
   use PDL::Math;
   use PDL::Fit::LM;
   use strict;


   ### fit using pdl&#39;s lmfit (Marquardt-Levenberg non-linear least squares fitting)
   ###
   ### `lmfit&#39; Syntax: 
   ###
   ### ($ym,$finalp,$covar,$iters)
   ###  = lmfit $x, $y, $sigma, \&amp;fn, $initp, {Maxiter =&gt; 300, Eps =&gt; 1e-3};
   ###
   ### Explanation of variables
   ### 
   ### OUTPUT
   ### $ym     = pdl of fitted values
   ### $finalp = pdl of parameters
   ### $covar  = covariance matrix
   ### $iters  = number of iterations actually used
   ###
   ### INPUT
   ### $x      = x data
   ### $y      = y data
   ### $sigma  = piddle of y-uncertainties for each value of $y (can be set to scalar 1 for equal weighting)
   ### \&amp;fn    = reference to function provided by user (more on this below)
   ### $initp  = initial values for floating parameters
   ###               (needs to be explicitly set prior to use of lmfit)
   ### Maxiter = maximum iterations
   ### Eps     = convergence criterion (maximum normalized change in Chi Sq.)

   ### Example:
   # make up experimental data:
   my $xdata = pdl sequence 5;
   my $ydata = pdl [1.1,1.9,3.05,4,4.9];

   # set initial prameters in a pdl (order in accord with fit function below)
   my $initp = pdl [0,1];

   # Weight all y data equally (else specify different uncertainties in a pdl)
   my $sigma = 1;

   # Use lmfit. Fourth input argument is reference to user-defined 
   # subroutine ( here \&amp;linefit ) detailed below.
   my ($yf,$pf,$cf,$if) = lmfit $xdata, $ydata, $sigma, \&amp;linefit, $initp;

   # Note output
   print &quot;\nXDATA\n$xdata\nY DATA\n$ydata\n\nY DATA FIT\n$yf\n\n&quot;;
   print &quot;Slope and Intercept\n$pf\n\nCOVARIANCE MATRIX\n$cf\n\n&quot;;
   print &quot;NUMBER ITERATIONS\n$if\n\n&quot;;


   # simple example of user defined fit function. Guidelines included on
   # how to write your own function subroutine.
   sub linefit {

           # leave this line as is
           my ($x,$par,$ym,$dyda) = @_;

           # $m and $c are fit parameters, internal to this function
           # call them whatever make sense to you, but replace (0..1)
           # with (0..x) where x is equal to your number of fit parameters
           # minus 1
           my ($m,$c) = map { $par-&gt;slice(&quot;($_)&quot;) } (0..1);

           # Write function with dependent variable $ym,
           # independent variable $x, and fit parameters as specified above.
           # Use the .= (dot equals) assignment operator to express the equality 
           # (not just a plain equals)
           $ym .= $m * $x + $c;

           # Edit only the (0..1) part to (0..x) as above
           my (@dy) = map {$dyda -&gt; slice(&quot;,($_)&quot;) } (0..1);

           # Partial derivative of the function with respect to first 
           # fit parameter ($m in this case). Again, note .= assignment 
           # operator (not just &quot;equals&quot;)
           $dy[0] .= $x;

           # Partial derivative of the function with respect to next 
           # fit parameter ($y in this case)
           $dy[1] .= 1;

           # Add $dy[ ] .= () lines as necessary to supply 
           # partial derivatives for all floating parameters.
   }</code></pre>

<h2 id="tlmfit">tlmfit</h2>

<p>threaded version of Levenberg-Marquardt fitting routine mfit</p>

<pre><code> tlmfit $x, $y, float(1)-&gt;dummy(0), $na, float(200), float(1e-4),
       $ym=null, $afit=null, \&amp;expdec;</code></pre>

<pre><code>  Signature: tlmfit(x(n);y(n);sigma(n);initp(m);iter();eps();[o] ym(n);[o] finalp(m);
           OtherPar =&gt; subref)</code></pre>

<p>a threaded version of <code>lmfit</code> by using perl threading. Direct threading in <code>lmfit</code> seemed difficult since we have an if condition in the iteration. In principle that can be worked around by using <code>where</code> but .... Send a threaded <code>lmfit</code> version if you work it out!</p>

<p>Since we are using perl threading here speed is not really great but it is just convenient to have a threaded version for many applications (no explicit for-loops required, etc). Suffers from some of the current limitations of perl level threading.</p>

<h1 id="BUGS">BUGS</h1>

<p>Not known yet.</p>

<h1 id="AUTHOR">AUTHOR</h1>

<p>This file copyright (C) 1999, Christian Soeller (c.soeller@auckland.ac.nz). All rights reserved. There is no warranty. You are allowed to redistribute this software documentation under certain conditions. For details, see the file COPYING in the PDL distribution. If this file is separated from the PDL distribution, the copyright notice should be included in the file.</p>


</body>

</html>


